apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

if (openAspectJ) {
    apply plugin: 'android-aspectjx'
    //或使用如下
    //apply plugin: 'com.hujiang.android-aspectjx'
}

kapt {
    useBuildCache = true
    javacOptions {
        option("-Xmaxerrs", 500)
    }
    //开启databinding后如果使用kapt，需要进行如下配置
    //generateStubs = true
}

android {
    compileSdkVersion proVer.compileSdk
    defaultConfig {
        applicationId "com.jsongo.mybasefrm.demo"
        targetSdkVersion proVer.targetSdk
        minSdkVersion proVer.minSdk
        versionCode libConf.demo.verCode
        versionName libConf.demo.verName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
//            abiFilters "armeabi", "armeabi-v7a"
            abiFilters "armeabi", "armeabi-v7a", "x86", "x86_64"
        }
        flavorDimensions "default"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests {
            //单元测试引用android资源
            includeAndroidResources = true
        }
    }
    //签名文件配置，开发平台自动打包配置，不允许修改。如果编译报错请检查根目录gradle.properties是否配置了相关参数。
    signingConfigs {
        config_release {
            storeFile file(RELEASE_STORE_FILE)
            keyAlias RELEASE_KEY_ALIAS
            storePassword RELEASE_KEY_PASSWORD
            keyPassword RELEASE_STORE_PASSWORD
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.config_release
            //开启测试覆盖率
            testCoverageEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', '../proguard-rules.pro', '../tencentx5_proguard.pro'

            ext.enableCrashlytics = false
        }
        release {
            signingConfig signingConfigs.config_release
            //开启资源压缩，必须开启混淆
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', '../proguard-rules.pro', '../tencentx5_proguard.pro'
//            debuggable true
        }
    }

    productFlavors {
        dev1 {
            applicationIdSuffix '.dev1'
            versionNameSuffix '-dev1'
            manifestPlaceholders = [app_name: "sapp1"]
        }
        dev2 {
            applicationIdSuffix '.dev2'
            versionNameSuffix '-dev2'
            manifestPlaceholders = [app_name: "sapp2"]
        }
    }

    dexOptions {
        //增加编译内存
        javaMaxHeapSize "4g"
        jumboMode = true
        preDexLibraries = true
        maxProcessCount = 8
    }
    lintOptions {
        //忽略打包时错误
        checkReleaseBuilds false
        abortOnError false
    }
    aaptOptions {
        cruncherEnabled false
    }
}

dependencies {

    implementation "com.jsongo.basefrm:annotation:${libConf.annotation.aarVer}"
    kapt "com.jsongo.basefrm:processor:${libConf.processor.aarVer}"
    if (libConf.useOnlineAAR) {
//        implementation "com.jsongo.basefrm:annotation:${libConf.annotation.aarVer}"
        implementation "com.jsongo.basefrm:core:${libConf.core.aarVer}"
        implementation "com.jsongo.basefrm:ajs:${libConf.ajs.aarVer}"
        implementation "com.jsongo.basefrm:ui:${libConf.ui.aarVer}"
        implementation "com.jsongo.basefrm:ui_enhance:${libConf.ui_enhance.aarVer}"
        implementation "com.jsongo.basefrm:mobileim:${libConf.mobileim.aarVer}"
//        kapt "com.jsongo.basefrm:processor:${libConf.processor.aarVer}"
    } else {
//        implementation project(path: ':annotation')
        implementation project(path: ':core')
        implementation project(path: ':ajs')
        implementation project(path: ':ui')
        implementation project(path: ':ui-enhance')
        implementation project(path: ':mobileim')
//        kapt project(path: ':processor')
    }

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //测试使用类库
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}
